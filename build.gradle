// -*- coding: utf-8; mode: groovy -*-

buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "com.diffplug.gradle.spotless:spotless:1.3.3"
		classpath "de.thetaphi:forbiddenapis:2.2"
		classpath "de.aaschmid:gradle-cpd-plugin:1.0"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
	}
}

// basic plugins
apply plugin: "java"
apply plugin: "spring-boot"

// custom configuration
apply from: 'gradle/version.gradle'
apply from: 'gradle/resolveDependencies.gradle'

// code quality plugins
apply plugin: "checkstyle"
apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "cpd"
apply plugin: "jacoco"
apply plugin: "de.thetaphi.forbiddenapis"
apply plugin: "com.diffplug.gradle.spotless"

// code quality configuration
apply from: 'gradle/quality/checkstyle.gradle'
apply from: 'gradle/quality/findbugs.gradle'
apply from: 'gradle/quality/pmd.gradle'
apply from: 'gradle/quality/cpd.gradle'
apply from: 'gradle/quality/jacoco.gradle'
apply from: 'gradle/quality/forbiddenapis.gradle'
apply from: 'gradle/quality/spotless.gradle'

// basic configuration
group = "jp.classmethod.sparrow"
ext.artifactId = "sparrow"

// compiler configuration
sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(AbstractCompile) each {
	it.options.encoding = "UTF-8"
}

compileJava {
	options.compilerArgs << "-Werror"
	options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-deprecation"
}

springBoot {
	executable = true
}

//======== local test and run ========
def springProfilesActive = project.hasProperty("springProfiles") ? project.springProfiles : "personal"
test {
	testLogging.exceptionFormat = 'full'
	beforeTest {
		logger.lifecycle("{} > {}", it.parent.name, it.name)
	}
	systemProperties["spring.profiles.active"] = springProfilesActive
}
bootRun.systemProperties["spring.profiles.active"] = springProfilesActive


// libraries
repositories {
	jcenter()
	mavenCentral()
	maven { url "http://maven.xet.jp/release" } // for spring-data-mirage
}

dependencies {
	// spring
	compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
	compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
	compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
	compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
	
	compile "org.springframework.data:spring-data-mirage:$springDataMirageVersion"
	
	// logger
	compile("org.slf4j:slf4j-api:1.7.21") { force = true }
	compile "ch.qos.logback:logback-classic:1.1.7"

	// others
	compile "jp.xet.spar-wings:spar-wings-httpexceptions:$sparWingsVersion"
	compile "jp.xet.spar-wings:spar-wings-spring-data-chunk:$sparWingsVersion"
	compile "org.apache.httpcomponents:httpclient:4.5.3"
	compile "org.apache.commons:commons-lang3:3.0"
	compile "org.flywaydb:flyway-core:$flywayVersion"
	compileOnly "org.projectlombok:lombok:$lombokVersion"
	
	compile "com.h2database:h2:1.4.195"
	compile "mysql:mysql-connector-java:5.1.42"
	
	// testing
	testCompileOnly "org.projectlombok:lombok:$lombokVersion" // http://qiita.com/gillax/items/1cbcff003384087f2db1
	testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
	testCompile "org.assertj:assertj-core"
	testCompile "com.jayway.jsonpath:json-path-assert:$jsonPathAssertVersion"
}

// wrapper
task wrapper(type: Wrapper) {
	gradleVersion = "2.12"
}
